plugins {
    id 'com.android.application'
}

def getGitHash = { ->
    def stdout = new ByteArrayOutputStream()
    exec {
        commandLine 'git', 'rev-parse', '--short', 'HEAD'
        standardOutput = stdout
    }
    return stdout.toString().trim()
}

android {
    defaultConfig {
        applicationId "deltazero.amarok"
        minSdkVersion 26
        targetSdkVersion 34
        compileSdk 34
        versionCode 72
        versionName "0.8.6b1"

        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
    }

    if (project.hasProperty("RELEASE_STORE_FILE")) {
        signingConfigs {
            release {
                storeFile file(RELEASE_STORE_FILE)
                storePassword RELEASE_STORE_PASSWORD
                keyAlias RELEASE_KEY_ALIAS
                keyPassword RELEASE_KEY_PASSWORD

                enableV2Signing = true
                enableV3Signing = true
                enableV4Signing = true
            }
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            if (project.hasProperty("RELEASE_STORE_FILE")) {
                signingConfig signingConfigs.release
            }
        }
        debug {
            applicationIdSuffix ".debug"
            versionNameSuffix "-${getGitHash()}"
            debuggable true
        }
    }

    productFlavors {
        flavorDimensions = ['version']
        appcenter {
            dimension 'version'
        }
        foss {
            dimension 'version'
            applicationIdSuffix '.foss'
        }
    }

    sourceSets {
        appcenter {
            java.srcDirs = ['src/appcenter/java']
        }
        foss {
            java.srcDirs = ['src/foss/java']
        }
    }

    applicationVariants.configureEach { variant ->
        def flavorName = variant.productFlavors[0].name
        if (flavorName == "appcenter") {
            variant.outputs.configureEach { output ->
                outputFileName = "Amarok-v${variant.versionName}.apk"
            }
        } else {
            variant.outputs.configureEach { output ->
                outputFileName = "Amarok-v${variant.versionName}-${flavorName}.apk"
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_17
        targetCompatibility JavaVersion.VERSION_17
    }

    namespace 'deltazero.amarok'
    buildFeatures {
        buildConfig true
    }
}

repositories {
    mavenCentral()
}

dependencies {
    def shizukuVersion = '13.1.5'
    implementation "dev.rikka.shizuku:api:${shizukuVersion}"
    implementation "dev.rikka.shizuku:provider:${shizukuVersion}"

    def appCenterSdkVersion = '5.0.4'
    appcenterImplementation "com.microsoft.appcenter:appcenter-analytics:${appCenterSdkVersion}"
    appcenterImplementation "com.microsoft.appcenter:appcenter-crashes:${appCenterSdkVersion}"
    appcenterImplementation "com.microsoft.appcenter:appcenter-distribute:${appCenterSdkVersion}"

    implementation 'dev.rikka.rikkax.material:material-preference:2.0.0'
    implementation 'dev.rikka.rikkax.appcompat:appcompat:1.6.1'
    implementation 'io.github.iamr0s:Dhizuku-API:2.4'
    implementation 'com.github.getActivity:XXPermissions:18.5'
    implementation 'com.github.getActivity:EasyWindow:10.6'
    implementation 'com.github.heruoxin.Delegated-Scopes-Manager:client:master-SNAPSHOT'
    implementation 'com.github.takusemba:spotlight:2.0.5'
    implementation 'com.github.topjohnwu.libsu:core:5.2.2'
    implementation 'com.google.android.material:material:1.11.0'
    implementation 'com.jonathanfinerty.once:once:1.3.1'
    implementation 'com.kizitonwose.calendar:view:2.4.1'
    implementation 'nl.dionsegijn:konfetti-xml:2.0.4'
    implementation 'androidx.biometric:biometric:1.1.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.2.0'
    implementation 'androidx.swiperefreshlayout:swiperefreshlayout:1.1.0'
    implementation 'androidx.preference:preference:1.2.1'
}

configurations.configureEach {
    exclude group: 'androidx.appcompat', module: 'appcompat'
}

apply plugin: 'dev.rikka.tools.autoresconfig'
autoResConfig {
    generateClass = true
    generatedClassFullName = "deltazero.amarok.utils.LangList"
    generateRes = true
    generatedResPrefix = null
    generatedArrayFirstItem = "SYSTEM"
    generateLocaleConfig = true
}
